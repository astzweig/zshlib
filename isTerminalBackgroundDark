#!/usr/bin/env zsh
# vi: set expandtab ft=zsh tw=80 ts=2
# Based on https://gist.github.com/blueyed/c8470c2aad3381c33ea3
declare -gx _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS=

function isTerminalBackgroundDark() {
  test -t 1 || return 10
  if [ -z "${_ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS}" ]; then
    local answer hexrgb brightness weights onebyte hexvalue weight
    if [ -t 0 ]; then
      traps add zshlib-isTerminalBackgroundDark-reset-stty "stty $(stty -g)"
      trap "traps call exit; traps remove zshlib-isTerminalBackgroundDark-reset-stty" EXIT
      stty raw -echo min 0 time 3
    fi
    print -n "\033]11;?\033\\"
    read -r answer
    [ -n ${answer} ] || return 0
    hexrgb=(${(s./.)"${${answer#*:}//[^rg:\/A-Fa-f0-9]/}"}) weights=(.241 .691 .068) onebyte=256
    integer brightness=0
    for hexvalue weight in ${hexrgb:^weights}; do
      (( brightness+=0x$hexvalue / onebyte * weight ))
    done
    _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS="${brightness}"
  fi
  (( _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS <= 130 ))
}

if [[ "${ZSH_EVAL_CONTEXT}" == toplevel ]]; then
  declare -A traps=()
  _DIR="${0:A:h}"
  whence traps >&! /dev/null || source "${_DIR}/traps"
  trap 'code=$?; traps call; return $code' INT TERM EXIT
  isTerminalBackgroundDark "$@"
elif [[ "${ZSH_EVAL_CONTEXT}" == *loadautofunc ]]; then
  isTerminalBackgroundDark "$@"
fi
