#!/usr/bin/env zsh
# vi: set expandtab ft=zsh tw=80 ts=2
# Based on https://gist.github.com/blueyed/c8470c2aad3381c33ea3
declare -gx _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS=

function isTerminalBackgroundDark() {
  test -t 1 || return 10
  if [ -z "${_ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS}" ]; then
    local answer hexrgb brightness weights onebyte hexvalue weight
    if [ -t 0 ]; then
      trap "stty $(stty -g)" INT TERM EXIT
      stty raw -echo min 0 time 1
    fi
    print -n "\033]11;?\033\\"
    read -r answer
    hexrgb=(${(s./.)"${${answer#*:}//[^rg:\/A-Fa-f0-9]/}"}) weights=(.241 .691 .068) onebyte=256
    integer brightness=0
    for hexvalue weight in ${hexrgb:^weights}; do
      (( brightness+=0x$hexvalue / onebyte * weight ))
    done
    _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS="${brightness}"
  fi
  (( _ZSHLIB_ISTERMINALBACKGROUNDDARK_BRIGHTNESS <= 130 ))
}

if [[ "${ZSH_EVAL_CONTEXT}" == toplevel || "${ZSH_EVAL_CONTEXT}" == *loadautofunc ]]; then
  isTerminalBackgroundDark "$@"
fi
