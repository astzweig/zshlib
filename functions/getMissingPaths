#!/usr/bin/env zsh
# vi: set expandtab ft=zsh tw=80 ts=2

function getMissingPaths() {
	eval "`docopts -f -V - -h - : "$@" <<- USAGE
	Usage: $0 [options] <pathitem>

	Print each prefix of pathitem that does not exist to stdout.

	Options:
	  -f, --file   Do not check last segment of pathitem.
	----
	$0 0.1.0
	Copyright (C) 2022 Rezart Qelibari, Astzweig GmbH & Co. KG
	License EUPL-1.2. There is NO WARRANTY, to the extent permitted by law.
	USAGE`"
	local segments=(${(s./.)pathitem}) firstItem=${"${(@s./.)pathitem}"[1]} item=
	[[ ${file} = true ]] && segments=(${segments:0:-1})
	[[ -z ${firstItem} ]] && firstItem=/ || firstItem=

	until item=${firstItem}${(j./.)segments} && test -d ${item}; do
		segments=(${segments:0:-1})
		print -- ${item}
	done
}

if [[ "${ZSH_EVAL_CONTEXT}" == toplevel || "${ZSH_EVAL_CONTEXT}" == *loadautofunc ]]; then
	getMissingPaths "$@"
fi
